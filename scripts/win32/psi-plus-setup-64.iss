[Components]
Name: "bin64"; Description: "Psi+"; Types: full compact custom; Flags: exclusive
Name: "bin64w"; Description: "Psi+ WebEngine"; Types: full compact custom; Flags: exclusive
Name: "common"; Description: "Psi+ common files"; Types: full custom compact; Flags: fixed
;Plugins
Name: "plugins"; Description: "Psi+ plugins"; Types: full
Name: "plugins\attentionplugin"; Description: "Attention plugin"; Types: full
Name: "plugins\autoreplyplugin"; Description: "Auto Reply plugin"; Types: full
Name: "plugins\battleshipgameplugin"; Description: "Battleship Game plugin"; Types: full
Name: "plugins\birthdayreminderplugin"; Description: "Birthday Reminder plugin"; Types: full
Name: "plugins\chessplugin"; Description: "Chess plugin"; Types: full
Name: "plugins\cleanerplugin"; Description: "Cleaner plugin"; Types: full
Name: "plugins\clientswitcherplugin"; Description: "Client Switcher plugin"; Types: full
Name: "plugins\conferenceloggerplugin"; Description: "Conference Logger plugin"; Types: full
Name: "plugins\contentdownloaderplugin"; Description: "Content Downloader plugin"; Types: full
Name: "plugins\enummessagesplugin"; Description: "Enum Messages plugin"; Types: full
Name: "plugins\extendedmenuplugin"; Description: "Extended Menu plugin"; Types: full
Name: "plugins\extendedoptionsplugin"; Description: "Extended Options plugin"; Types: full
Name: "plugins\gomokugameplugin"; Description: "Gomoku Game plugin"; Types: full
Name: "plugins\historykeeperplugin"; Description: "History Keeper plugin"; Types: full
Name: "plugins\imageplugin"; Description: "Image plugin"; Types: full
Name: "plugins\imagepreviewplugin"; Description: "Image Preview plugin"; Types: full
Name: "plugins\jabberdiskplugin"; Description: "Jabber Disk plugin"; Types: full
Name: "plugins\juickplugin"; Description: "Juick plugin"; Types: full
Name: "plugins\mediaplugin"; Description: "PsiMedia plugin"; Types: full compact
Name: "plugins\messagefilterplugin"; Description: "Message Filter plugin"; Types: full
Name: "plugins\omemoplugin"; Description: "OMEMO plugin"; Types: full
Name: "plugins\openpgpplugin"; Description: "OpenPGP plugin"; Types: full
Name: "plugins\otrplugin"; Description: "OTR plugin"; Types: full
Name: "plugins\pepchangenotifyplugin"; Description: "PEP Change Notify plugin"; Types: full
Name: "plugins\qipxstatusesplugin"; Description: "Qip X-Statuses plugin"; Types: full
Name: "plugins\screenshotplugin"; Description: "Screenshot plugin"; Types: full
Name: "plugins\stopspamplugin"; Description: "Stop Spam plugin"; Types: full
Name: "plugins\storagenotesplugin"; Description: "Storage Notes plugin"; Types: full
Name: "plugins\translateplugin"; Description: "Translate plugin"; Types: full
Name: "plugins\videostatusplugin"; Description: "Video Status Changer plugin"; Types: full
Name: "plugins\watcherplugin"; Description: "Watcher plugin"; Types: full
;Spellcheck dictionaries
Name: "dicts"; Description: "{cm:spelldicdesc}"; Types: full
Name: "dicts\af_ZA"; Description: "Afrikaans"; Types: full
Name: "dicts\an_ES"; Description: "Aragonese"; Types: full
Name: "dicts\ar"; Description: "عربي"; Types: full
Name: "dicts\be_BY"; Description: "Беларуская мова"; Types: full
Name: "dicts\bg_BG"; Description: "Български"; Types: full
Name: "dicts\bn_BD"; Description: "Bengali"; Types: full
Name: "dicts\br_FR"; Description: "Brezhoneg"; Types: full
Name: "dicts\bs_BA"; Description: "Bosanski"; Types: full
Name: "dicts\cs_CZ"; Description: "Čeština"; Types: full
Name: "dicts\da_DK"; Description: "Dansk"; Types: full
Name: "dicts\de_AT_frami"; Description: "Deutsch (Österreich)"; Types: full
Name: "dicts\de_CH_frami"; Description: "Deutsch (Schweiz)"; Types: full
Name: "dicts\de_DE_frami"; Description: "Deutsch (Deutschland)"; Types: full
Name: "dicts\el_GR"; Description: "Ελληνικά"; Types: full
Name: "dicts\en_AU"; Description: "English (Australia)"; Types: full
Name: "dicts\en_CA"; Description: "English (Canada)"; Types: full
Name: "dicts\en_GB"; Description: "English (United Kingdom)"; Types: full
Name: "dicts\en_US"; Description: "English (United States)"; Types: full compact
Name: "dicts\en_ZA"; Description: "English (South Africa)"; Types: full
Name: "dicts\es_ANY"; Description: "Español"; Types: full
Name: "dicts\et_EE"; Description: "Eesti keel"; Types: full
Name: "dicts\fr"; Description: "Français"; Types: full
Name: "dicts\gl_ES"; Description: "Galego"; Types: full
Name: "dicts\gu_IN"; Description: "Gujarati (India)"; Types: full
Name: "dicts\he_IL"; Description: "עברית"; Types: full
Name: "dicts\hi_IN"; Description: "Hindi"; Types: full
Name: "dicts\hr_HR"; Description: "Hrvatski"; Types: full
Name: "dicts\hu_HU"; Description: "Magyar"; Types: full
Name: "dicts\is_dic"; Description: "Íslenska"; Types: full
Name: "dicts\it_IT"; Description: "Italiano"; Types: full
Name: "dicts\kmr_Latn"; Description: "Kurdish (Latin)"; Types: full
Name: "dicts\lo_LA"; Description: "Lao"; Types: full
Name: "dicts\lt"; Description: "Lietuvių kalba"; Types: full
Name: "dicts\lv_LV"; Description: "Latviešu"; Types: full
Name: "dicts\nb_NO"; Description: "Norsk (Bokmål)"; Types: full
Name: "dicts\ne_NP"; Description: "Nepali"; Types: full
Name: "dicts\nl_NL"; Description: "Nederlands"; Types: full
Name: "dicts\nn_NO"; Description: "Norsk (Nynorsk)"; Types: full
Name: "dicts\oc_FR"; Description: "Occitan"; Types: full
Name: "dicts\pl_PL"; Description: "Polski"; Types: full
Name: "dicts\pt_BR"; Description: "Português do Brasil"; Types: full
Name: "dicts\pt_PT"; Description: "Português de Portugal"; Types: full
Name: "dicts\ro_RO"; Description: "Română"; Types: full
Name: "dicts\ru_RU"; Description: "Русский"; Types: full;
Name: "dicts\si_LK"; Description: "Sinhala"; Types: full
Name: "dicts\sk_SK"; Description: "Slovenčina"; Types: full
Name: "dicts\sl_SI"; Description: "Slovenščina"; Types: full
Name: "dicts\sr_Latn"; Description: "Српски (Latin)"; Types: full
Name: "dicts\sr"; Description: "Српски"; Types: full
Name: "dicts\sv_FI"; Description: "Svenska (Finland)"; Types: full
Name: "dicts\sv_SE"; Description: "Svenska (Sverige)"; Types: full
Name: "dicts\sw_TZ"; Description: "Kiswahili (Tanzania)"; Types: full
Name: "dicts\te_IN"; Description: "Telugu (India)"; Types: full
Name: "dicts\th_TH"; Description: "Thai"; Types: full
Name: "dicts\uk_UA"; Description: "Українська"; Types: full;
Name: "dicts\vi_VN"; Description: "Tiếng Việt"; Types: full

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "ukrainian"; MessagesFile: "compiler:Languages\Ukrainian.isl"

[Setup]
#define MyAppName "psi-plus"
#define MyAppExeName "psi-plus.exe"
#define WorkDir "C:\build\Installer"
#define ResDir "C:\build\resources"
#define PsiPSrcDir "C:\build\psi-plus-snapshots"
#define AppVer GetFileVersion("C:\build\Installer\bin64-qt5\psi-plus.exe")
#define CurrYear GetDateTimeString('yyyy', '', '')
AppName=Psi+
AppVersion={#AppVer}-x64
AppCopyright=© 2008-{#CurrYear} Psi+ Project
VersionInfoVersion={#AppVer}
VersionInfoCompany=Psi+ Project
VersionInfoTextVersion={#AppVer}-x64
VersionInfoCopyright=© 2008-{#CurrYear} Psi+ Project
VersionInfoProductName=Psi+
VersionInfoProductVersion={#AppVer}
VersionInfoProductTextVersion={#AppVer}-x64
AppId={{751DD547-F64F-44D8-8304-5643E71D409B}
AppPublisher=Psi+ Project
AppPublisherURL=https://psi-plus.com/
LicenseFile={#PsiPSrcDir}\COPYING
SetupIconFile={#PsiPSrcDir}\win32\app-plus.ico
OutputDir={#WorkDir}
OutputBaseFilename=psi-plus-{#AppVer}-x64-setup
Compression=lzma2/ultra64
InternalCompressLevel=ultra64
DefaultDirName={pf}\Psi-plus
ArchitecturesAllowed=x64 ia64
UninstallDisplayName=Psi+ {#AppVer}
DefaultGroupName=Psi+ (x64)
AppSupportURL=https://github.com/psi-plus/main/issues
AppUpdatesURL=https://drive.google.com/drive/folders/0B_6HeEGc8_zqNUZxSzgyaVpMcmM?resourcekey=0-1EEjvrBB8y9BoDfOzQ2Shw&usp=sharingd
UninstallDisplayIcon={app}\{#MyAppExeName}
ArchitecturesInstallIn64BitMode=x64 ia64
MinVersion=0,6.1
DisableProgramGroupPage=auto
PrivilegesRequired=poweruser
UsePreviousGroup=False
AllowNoIcons=True
DisableWelcomePage=False
ShowTasksTreeLines=True
WizardStyle=modern
SetupLogging=True
;more consts
#define SetupTypeReg "Inno Setup: Setup Type"
#define SelectedComponentsReg "Inno Setup: Selected Components"
#define DeSelectedComponentsReg "Inno Setup: Deselected Components"
#define SelectedTasksReg "Inno Setup: Selected Tasks"
#define AppPathReg "Inno Setup: App Path"
#define NoIconsDWReg "Inno Setup: No Icons"

[Files]
Source: "{#PsiPSrcDir}\certs\*"; DestDir: "{app}\certs"; Flags: ignoreversion recursesubdirs skipifsourcedoesntexist; Components: common
Source: "{#PsiPSrcDir}\iconsets\roster\*.jisp"; DestDir: "{app}\iconsets\roster"; Flags: ignoreversion recursesubdirs skipifsourcedoesntexist; Components: common
Source: "{#PsiPSrcDir}\sound\*"; DestDir: "{app}\sound"; Flags: ignoreversion recursesubdirs skipifsourcedoesntexist; Components: common
Source: "{#PsiPSrcDir}\skins\*"; DestDir: "{app}\skins"; Flags: ignoreversion recursesubdirs skipifsourcedoesntexist; Components: common
Source: "{#PsiPSrcDir}\client_icons.txt"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs skipifsourcedoesntexist; Components: common
Source: "{#PsiPSrcDir}\CHANGELOG"; DestDir: "{app}"; DestName: "CHANGELOG.TXT"; Flags: ignoreversion skipifsourcedoesntexist; Components: common
Source: "{#WorkDir}\translations-64-qt5\*"; DestDir: "{app}\translations"; Flags: ignoreversion recursesubdirs; Components: common
Source: "{#WorkDir}\lib64-qt5\*"; DestDir: "{app}"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: common
Source: "{#WorkDir}\psimedia-qt5\*"; DestDir: "{app}"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: plugins\mediaplugin
Source: "{#WorkDir}\webengine64-qt5\*"; DestDir: "{app}"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: bin64w
Source: "{#WorkDir}\bin64-qt5\psi-plus.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: bin64
Source: "{#WorkDir}\bin64-w-qt5\psi-plus.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: bin64w
Source: "{#PsiPSrcDir}\COPYING"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist; Components: common
Source: "common\README.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: common
;Dicts
Source: "common\myspell\dicts\af_ZA.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\af_ZA
Source: "common\myspell\dicts\af_ZA.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\af_ZA
Source: "common\myspell\dicts\an_ES.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\an_ES
Source: "common\myspell\dicts\an_ES.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\an_ES
Source: "common\myspell\dicts\ar.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\ar
Source: "common\myspell\dicts\ar.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\ar
Source: "common\myspell\dicts\be_BY.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\be_BY
Source: "common\myspell\dicts\be_BY.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\be_BY
Source: "common\myspell\dicts\bg_BG.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\bg_BG
Source: "common\myspell\dicts\bg_BG.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\bg_BG
Source: "common\myspell\dicts\bn_BD.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\bn_BD
Source: "common\myspell\dicts\bn_BD.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\bn_BD
Source: "common\myspell\dicts\br_FR.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\br_FR
Source: "common\myspell\dicts\br_FR.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\br_FR
Source: "common\myspell\dicts\bs_BA.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\bs_BA
Source: "common\myspell\dicts\bs_BA.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\bs_BA
Source: "common\myspell\dicts\cs_CZ.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\cs_CZ
Source: "common\myspell\dicts\cs_CZ.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\cs_CZ
Source: "common\myspell\dicts\da_DK.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\da_DK
Source: "common\myspell\dicts\da_DK.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\da_DK
Source: "common\myspell\dicts\de_AT_frami.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\de_AT_frami
Source: "common\myspell\dicts\de_AT_frami.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\de_AT_frami
Source: "common\myspell\dicts\de_CH_frami.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\de_CH_frami
Source: "common\myspell\dicts\de_CH_frami.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\de_CH_frami
Source: "common\myspell\dicts\de_DE_frami.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\de_DE_frami
Source: "common\myspell\dicts\de_DE_frami.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\de_DE_frami
Source: "common\myspell\dicts\el_GR.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\el_GR
Source: "common\myspell\dicts\el_GR.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\el_GR
Source: "common\myspell\dicts\en_AU.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_AU
Source: "common\myspell\dicts\en_AU.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_AU
Source: "common\myspell\dicts\en_CA.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_CA
Source: "common\myspell\dicts\en_CA.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_CA
Source: "common\myspell\dicts\en_GB.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_GB
Source: "common\myspell\dicts\en_GB.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_GB
Source: "common\myspell\dicts\en_US.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_US
Source: "common\myspell\dicts\en_US.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_US
Source: "common\myspell\dicts\en_ZA.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_ZA
Source: "common\myspell\dicts\en_ZA.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\en_ZA
Source: "common\myspell\dicts\es_ANY.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\es_ANY
Source: "common\myspell\dicts\es_ANY.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\es_ANY
Source: "common\myspell\dicts\et_EE.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\et_EE
Source: "common\myspell\dicts\et_EE.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\et_EE
Source: "common\myspell\dicts\fr.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\fr
Source: "common\myspell\dicts\fr.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\fr
Source: "common\myspell\dicts\gl_ES.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\gl_ES
Source: "common\myspell\dicts\gl_ES.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\gl_ES
Source: "common\myspell\dicts\gu_IN.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\gu_IN
Source: "common\myspell\dicts\gu_IN.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\gu_IN
Source: "common\myspell\dicts\he_IL.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\he_IL
Source: "common\myspell\dicts\he_IL.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\he_IL
Source: "common\myspell\dicts\hi_IN.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\hi_IN
Source: "common\myspell\dicts\hi_IN.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\hi_IN
Source: "common\myspell\dicts\hr_HR.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\hr_HR
Source: "common\myspell\dicts\hr_HR.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\hr_HR
Source: "common\myspell\dicts\hu_HU.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\hu_HU
Source: "common\myspell\dicts\hu_HU.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\hu_HU
Source: "common\myspell\dicts\is.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\is_dic
Source: "common\myspell\dicts\is.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\is_dic
Source: "common\myspell\dicts\it_IT.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\it_IT
Source: "common\myspell\dicts\it_IT.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\it_IT
Source: "common\myspell\dicts\kmr_Latn.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\kmr_Latn
Source: "common\myspell\dicts\kmr_Latn.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\kmr_Latn
Source: "common\myspell\dicts\lo_LA.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\lo_LA
Source: "common\myspell\dicts\lo_LA.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\lo_LA
Source: "common\myspell\dicts\lt.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\lt
Source: "common\myspell\dicts\lt.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\lt
Source: "common\myspell\dicts\lv_LV.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\lv_LV
Source: "common\myspell\dicts\lv_LV.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\lv_LV
Source: "common\myspell\dicts\nb_NO.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\nb_NO
Source: "common\myspell\dicts\nb_NO.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\nb_NO
Source: "common\myspell\dicts\ne_NP.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\ne_NP
Source: "common\myspell\dicts\ne_NP.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\ne_NP
Source: "common\myspell\dicts\nl_NL.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\nl_NL
Source: "common\myspell\dicts\nl_NL.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\nl_NL
Source: "common\myspell\dicts\nn_NO.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\nn_NO
Source: "common\myspell\dicts\nn_NO.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\nn_NO
Source: "common\myspell\dicts\oc_FR.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\oc_FR
Source: "common\myspell\dicts\oc_FR.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\oc_FR
Source: "common\myspell\dicts\pl_PL.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\pl_PL
Source: "common\myspell\dicts\pl_PL.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\pl_PL
Source: "common\myspell\dicts\pt_BR.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\pt_BR
Source: "common\myspell\dicts\pt_BR.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\pt_BR
Source: "common\myspell\dicts\pt_PT.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\pt_PT
Source: "common\myspell\dicts\pt_PT.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\pt_PT
Source: "common\myspell\dicts\ro_RO.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\ro_RO
Source: "common\myspell\dicts\ro_RO.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\ro_RO
Source: "common\myspell\dicts\ru_RU.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\ru_RU
Source: "common\myspell\dicts\ru_RU.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\ru_RU
Source: "common\myspell\dicts\si_LK.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\si_LK
Source: "common\myspell\dicts\si_LK.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\si_LK
Source: "common\myspell\dicts\sk_SK.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sk_SK
Source: "common\myspell\dicts\sk_SK.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sk_SK
Source: "common\myspell\dicts\sl_SI.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sl_SI
Source: "common\myspell\dicts\sl_SI.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sl_SI
Source: "common\myspell\dicts\sr-Latn.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sr_Latn
Source: "common\myspell\dicts\sr-Latn.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sr_Latn
Source: "common\myspell\dicts\sr.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sr
Source: "common\myspell\dicts\sr.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sr
Source: "common\myspell\dicts\sv_FI.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sv_FI
Source: "common\myspell\dicts\sv_FI.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sv_FI
Source: "common\myspell\dicts\sv_SE.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sv_SE
Source: "common\myspell\dicts\sv_SE.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sv_SE
Source: "common\myspell\dicts\sw_TZ.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sw_TZ
Source: "common\myspell\dicts\sw_TZ.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\sw_TZ
Source: "common\myspell\dicts\te_IN.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\te_IN
Source: "common\myspell\dicts\te_IN.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\te_IN
Source: "common\myspell\dicts\th_TH.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\th_TH
Source: "common\myspell\dicts\th_TH.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\th_TH
Source: "common\myspell\dicts\uk_UA.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\uk_UA
Source: "common\myspell\dicts\uk_UA.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\uk_UA
Source: "common\myspell\dicts\vi_VN.aff"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\vi_VN
Source: "common\myspell\dicts\vi_VN.dic"; DestDir: "{app}\myspell\dicts"; Flags: ignoreversion; Components: dicts\vi_VN
;Plugins
Source: "plugins64-qt5\attentionplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\attentionplugin
Source: "plugins64-qt5\autoreplyplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\autoreplyplugin
Source: "plugins64-qt5\battleshipgameplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\battleshipgameplugin
Source: "plugins64-qt5\birthdayreminderplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\birthdayreminderplugin
Source: "plugins64-qt5\chessplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\chessplugin
Source: "plugins64-qt5\cleanerplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\cleanerplugin
Source: "plugins64-qt5\clientswitcherplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\clientswitcherplugin
Source: "plugins64-qt5\conferenceloggerplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\conferenceloggerplugin
Source: "plugins64-qt5\contentdownloaderplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\contentdownloaderplugin
Source: "plugins64-qt5\enummessagesplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\enummessagesplugin
Source: "plugins64-qt5\extendedmenuplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\extendedmenuplugin
Source: "plugins64-qt5\extendedoptionsplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\extendedoptionsplugin
Source: "plugins64-qt5\gomokugameplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\gomokugameplugin
Source: "plugins64-qt5\historykeeperplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\historykeeperplugin
Source: "plugins64-qt5\imageplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\imageplugin
Source: "plugins64-qt5\imagepreviewplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\imagepreviewplugin and bin64
Source: "plugins64-qt5\jabberdiskplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\jabberdiskplugin
Source: "plugins64-qt5\juickplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\juickplugin
Source: "plugins64-qt5\mediaplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\mediaplugin
Source: "plugins64-qt5\messagefilterplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\messagefilterplugin
Source: "plugins64-qt5\omemoplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\omemoplugin
Source: "plugins64-qt5\openpgpplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\openpgpplugin
Source: "plugins64-qt5\otrplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\otrplugin
Source: "plugins64-qt5\pepchangenotifyplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\pepchangenotifyplugin
Source: "plugins64-qt5\qipxstatusesplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\qipxstatusesplugin
Source: "plugins64-qt5\screenshotplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\screenshotplugin
Source: "plugins64-qt5\stopspamplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\stopspamplugin
Source: "plugins64-qt5\storagenotesplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\storagenotesplugin
Source: "plugins64-qt5\translateplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\translateplugin
Source: "plugins64-qt5\videostatusplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\videostatusplugin
Source: "plugins64-qt5\watcherplugin.dll"; DestDir: "{app}\plugins"; Flags: ignoreversion; Components: plugins\watcherplugin
Source: "{#WorkDir}\vcredist\vc_redist.x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall; Components: common

[Run]
#define VCmsg "Installing Microsoft Visual C++ Redistributable...."
Filename: "{tmp}\vc_redist.x64.exe"; StatusMsg: "{#VCmsg}";Check: not VCinstalled
Filename: "{app}\psi-plus.exe"; WorkingDir: "{app}"; Flags: postinstall nowait skipifsilent

[Icons]
Name: "{userdesktop}\Psi+ (x64)"; Filename: "{app}\psi-plus.exe"; WorkingDir: "{app}"; Flags: createonlyiffileexists; IconFilename: "{app}\psi-plus.exe"; IconIndex: 0; Tasks: desktopitems
Name: "{group}\Psi+ (x64)"; Filename: "{app}\psi-plus.exe"; WorkingDir: "{app}"; Flags: createonlyiffileexists; IconFilename: "{app}\psi-plus.exe"
Name: "{group}\Uninstall Psi+ x64"; Filename: "{uninstallexe}"; Flags: createonlyiffileexists; IconFilename: "{app}\psi-plus.exe"; Tasks: uninstallitems

[ThirdParty]
UseRelativePaths=True

[Tasks]
Name: "desktopitems"; Description: "{cm:InstallDS}"; Components: common
Name: "uninstallitems"; Description: "{cm:InstallUN}"; Flags: unchecked; Components: common
Name: "createregentry"; Description: "{cm:CreateRE}"; Components: common

[CustomMessages]
english.InstallDS=Create Psi+ desktop shortcut
russian.InstallDS=Установить иконку Psi+ на Рабочий стол
french.InstallDS=Créer un raccourci de bureau
ukrainian.InstallDS=Встановити іконку Psi+ на робочий стіл
english.InstallUN=Create Uninstall icon in Program Group
russian.InstallUN=Создать иконку Uninstall в меню программы
french.InstallUN=Créer un raccourci de désinstallation dans le groupe du logiciel
ukrainian.InstallUN=Створити іконку Uninstall в меню програми
russian.CreateRE=Создать запись в реестре Windows для обработки ссылок XMPP
english.CreateRE=Create an entry in the Windows registry to handle XMPP links
french.CreateRE=Créer une entrée dans le registre Windows pour gérer les liens XMPP
ukrainian.CreateRE=Створити запис у реєстрі Windows для обробки XMPP-посилань
;version check
russian.vPart1=Обнаружена версия
english.vPart1=An already installed version
french.vPart1=Version
ukrainian.vPart1=Знайдено версію
russian.vPart2=программы "
english.vPart2=of the "
french.vPart2=déjà installée de "
ukrainian.vPart2=програми "
russian.vPart3=". Инсталлятор удалит версию
english.vPart3=" program was detected. The installer will uninstall
french.vPart3=" détectée. Le programme d'installation supprimera la version
ukrainian.vPart3=". Інсталятор видалить версію
russian.vPart4=. Вы хотите продолжить?
english.vPart4=version. Do you want to continue?
french.vPart4=. Voulez-vous continuer?
ukrainian.vPart4=. Бажаєте продовжити?
russian.spelldicdesc=Словари проверки правописания
english.spelldicdesc=Spell check dictionaries
french.spelldicdesc=Dictionnaires de vérification orthographique
ukrainian.spelldicdesc=Словники перевірки правопису


[Registry]
Root: "HKCR"; Subkey: "xmpp"; ValueType: string; ValueData: "URL:XMPP Protocol"; Flags: uninsdeletekeyifempty; Tasks: createregentry
Root: "HKCR"; Subkey: "xmpp"; ValueType: dword; ValueName: "EditFlags"; ValueData: "2"; Flags: uninsdeletekeyifempty; Tasks: createregentry
Root: "HKCR"; Subkey: "xmpp"; ValueType: string; ValueName: "URL Protocol"; Flags: uninsdeletekeyifempty; Tasks: createregentry
Root: "HKCR"; Subkey: "xmpp\DefaultIcon"; ValueType: string; ValueData: """{app}\psi-plus.exe"""; Flags: uninsclearvalue uninsdeletekeyifempty; Tasks: createregentry
Root: "HKCR"; Subkey: "xmpp\shell\open\command"; ValueType: string; ValueData: """{app}\psi-plus.exe"" ""--uri=%1"""; Flags: uninsclearvalue uninsdeletekeyifempty; Tasks: createregentry

[Messages]
english.WelcomeLabel2=This will install [name/ver] on your computer.%n%nIt is recommended that you close all other applications before continuing.%n%nWARNING! Since 1.5.1594 version installer do not contains iconpacks to reduce size. Please use Content Downloader plugin to install needed iconpacks
french.WelcomeLabel2=Cela installera [name/ver] sur votre ordinateur.%n%nIl est recommandé de fermer toutes les autres applications avant de continuer.%n%nAVERTISSEMENT ! Depuis la version 1.5.1594, le programme d'installation ne contient pas de packs d'icônes pour réduire la taille. Veuillez utiliser le plugin Content Downloader pour installer les packs d'icônes nécessaires 
russian.WelcomeLabel2=Это установит [name/ver] на ваш компьютер.%n%nПеред продолжением рекомендуется закрыть все другие приложения.%n%nВНИМАНИЕ! Начиная с версии 1.5.1594, инсталлятор не содержит пакеты иконок для уменьшения размера. Пожалуйста, воспользуйтесь Content Downloader плагином, чтобы установить необходимые иконки

[Code]

function InitializeUninstall(): Boolean;
  var ErrorCode: Integer;
begin
  ShellExec('open','taskkill.exe','/f /im {#MyAppExeName}','',SW_HIDE,ewNoWait,ErrorCode);
  ShellExec('open','tskill.exe',' {#MyAppName}','',SW_HIDE,ewNoWait,ErrorCode);
  result := True;
end;

function CompareVersion(str1, str2: String): Integer;
var
  temp1, temp2: String;
begin
    temp1 := str1;
    temp2 := str2;
    Result := CompareStr(temp1, temp2);
end;

function InitializeSetup(): Boolean;
var
  SelectedComponents: string;
  DeSelectedComponents: string;
  SetupType: string;
  SelectedTasks: string;
  AppPath: string;
  NoIconsDW: Cardinal;
  oldVersion: String;
  uninstaller: String;
  ErrorCode: Integer;
  vCurID      :String;
  vCurAppName :String;
begin
  vCurID:= '{#SetupSetting("AppId")}';
  vCurAppName:= '{#SetupSetting("AppName")}';
  //remove first "{" of ID
  vCurID:= Copy(vCurID, 2, Length(vCurID) - 1);
  //
  if RegKeyExists(HKEY_LOCAL_MACHINE,
    'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1') then
  begin
  //Backup setup config
    RegQueryStringValue(HKEY_LOCAL_MACHINE,
      'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1',
      ExpandConstant('{#SetupTypeReg}'), SetupType);
    RegQueryStringValue(HKEY_LOCAL_MACHINE,
      'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1',
      ExpandConstant('{#SelectedComponentsReg}'), SelectedComponents);
    RegQueryStringValue(HKEY_LOCAL_MACHINE,
      'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1',
      ExpandConstant('{#DeSelectedComponentsReg}'), DeSelectedComponents);
    RegQueryStringValue(HKEY_LOCAL_MACHINE,
      'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1',
      ExpandConstant('{#SelectedTasksReg}'), SelectedTasks);
    RegQueryStringValue(HKEY_LOCAL_MACHINE,
      'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1',
      ExpandConstant('{#AppPathReg}'), AppPath);
    RegQueryDWordValue(HKEY_LOCAL_MACHINE,
      'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1',
      ExpandConstant('{#NoIconsDWReg}'), NoIconsDW);
  //
    RegQueryStringValue(HKEY_LOCAL_MACHINE,
      'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1',
      'DisplayVersion', oldVersion);
      if MsgBox(ExpandConstant('{cm:vPart1}') + ' ' + oldVersion + ' ' + ExpandConstant('{cm:vPart2}') + vCurAppName + ExpandConstant('{cm:vPart3}') + ' ' + oldVersion + ' ' + ExpandConstant('{cm:vPart4}'),
        mbConfirmation, MB_YESNO) = IDNO then
      begin
        Result := False;
      end
      else
        begin
          if (InitializeUninstall() = true) then
          begin
            RegQueryStringValue(HKEY_LOCAL_MACHINE,
              'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1',
              'UninstallString', uninstaller);
            ShellExec('runas', uninstaller, '/SILENT', '', SW_HIDE, ewWaitUntilTerminated, ErrorCode);
            //Restore setup config
            RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1', ExpandConstant('{#SetupTypeReg}'), SetupType);
            RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1', ExpandConstant('{#SelectedComponentsReg}'), SelectedComponents);
            RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1', ExpandConstant('{#DeSelectedComponentsReg}'), DeSelectedComponents);
            RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1', ExpandConstant('{#SelectedTasksReg}'), SelectedTasks);
            RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1', ExpandConstant('{#AppPathReg}'), AppPath);
            RegWriteDWordValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + vCurID + '_is1', ExpandConstant('{#NoIconsDWReg}'), NoIconsDW);
            //
            Result := True;
          end;
        end; 
  end
  else
  begin
    Result := True;
  end;
end;

function VCinstalled: Boolean;
 // Function for Inno Setup Compiler
 // Returns True if same or later Microsoft Visual C++ 2019 Redistributable is installed, otherwise False.
 var
  major: Cardinal;
  minor: Cardinal;
  bld: Cardinal;
  rbld: Cardinal;
  key: String;
 begin
  Result := False;
  key := 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64';
  if RegQueryDWordValue(HKEY_LOCAL_MACHINE, key, 'Major', major) then begin
    if RegQueryDWordValue(HKEY_LOCAL_MACHINE, key, 'Minor', minor) then begin
      if RegQueryDWordValue(HKEY_LOCAL_MACHINE, key, 'Bld', bld) then begin
        if RegQueryDWordValue(HKEY_LOCAL_MACHINE, key, 'RBld', rbld) then begin
            Log('VC 2019 Redist Major is: ' + IntToStr(major) + ' Minor is: ' + IntToStr(minor) + ' Bld is: ' + IntToStr(bld) + ' Rbld is: ' + IntToStr(rbld));
            // Version info was found. Return true if later or equal to our 14.29.30135.0 redistributable
            // Note brackets required because of weird operator precendence
            Result := (major >= 14) and (minor >= 29) and (bld >= 30135) and (rbld >= 0)
        end;
      end;
    end;
  end;
 end;